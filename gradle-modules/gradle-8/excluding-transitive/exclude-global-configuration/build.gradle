plugins {
    id 'java'
}

group = 'com.baeldung'
version = 'unspecified'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

configurations.all {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
}


// exclude Globally In Configurations
configurations.configureEach {
    exclude group: 'org.junit.jupiter', module: 'junit-jupiter-api'
    exclude group: 'org.junit.jupiter', module: 'junit-jupiter-params'
    exclude group: 'org.junit.jupiter', module: 'junit-jupiter-engine'

    exclude group: 'com.google.guava', module : 'failureaccess'

    exclude group: 'org.apache.logging.log4j', module : 'log4j-api'
}

test {
    useJUnitPlatform()
}

tasks.register('logDependenciesGrouped') {
    doLast {

        Map<String, Set<String>> groupedDependencies = new TreeMap<>()
        configurations.each { configuration ->
            if (configuration.canBeResolved) {
                configuration.resolvedConfiguration.resolvedArtifacts.each { artifact ->
                    String group = artifact.moduleVersion.id.group
                    String dependency = "${artifact.moduleVersion.id.name}:${artifact.moduleVersion.id.version}"

                    // Gets or creates a set of dependencies for the group.
                    groupedDependencies.computeIfAbsent(group, { new TreeSet<>() }).add(dependency)
                }
            }
        }

        // Prints dependencies sorted per group
        groupedDependencies.each { group, dependencies ->
            println "+ $group"
            dependencies.each { dependency ->
                println "   module : $dependency"
            }
        }
    }
}